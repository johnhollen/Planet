<!doctype html>
<html>
  <head>
    <title> Procedural Ocean in Space </title>

    <link rel="stylesheet" type="text/css" href="./styles/oceanstyle.css">
    <link rel="stylesheet" href="//cdnjs.cloudflare.com/ajax/libs/highlight.js/8.8.0/styles/monokai_sublime.min.css">
  </head>
  <body>
    <!-- Shaders -->
    <!-- vertex shader -->
    <script type="x-shader/x-vertex" id="oceanVertexShader">
      #ifdef GL_ES
      precision highp float;
      #endif


      //Noise

      //
      // Description : Array and textureless GLSL 2D/3D/4D simplex
      //               noise functions.
      //      Author : Ian McEwan, Ashima Arts.
      //  Maintainer : ijm
      //     Lastmod : 20110822 (ijm)
      //     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
      //               Distributed under the MIT License. See LICENSE file.
      //               https://github.com/ashima/webgl-noise
      //

      vec3 mod289(vec3 x) {
        return x - floor(x * (1.0 / 289.0)) * 289.0;
      }

      vec4 mod289(vec4 x) {
        return x - floor(x * (1.0 / 289.0)) * 289.0;
      }

      vec4 permute(vec4 x) {
        return mod289(((x*34.0)+1.0)*x);
      }

      vec4 taylorInvSqrt(vec4 r)
      {
        return 1.79284291400159 - 0.85373472095314 * r;
      }

      float snoise(vec3 v)
      {
        const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
        const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);

        // First corner
        vec3 i  = floor(v + dot(v, C.yyy) );
        vec3 x0 =   v - i + dot(i, C.xxx) ;

        // Other corners
        vec3 g = step(x0.yzx, x0.xyz);
        vec3 l = 1.0 - g;
        vec3 i1 = min( g.xyz, l.zxy );
        vec3 i2 = max( g.xyz, l.zxy );

        //   x0 = x0 - 0.0 + 0.0 * C.xxx;
        //   x1 = x0 - i1  + 1.0 * C.xxx;
        //   x2 = x0 - i2  + 2.0 * C.xxx;
        //   x3 = x0 - 1.0 + 3.0 * C.xxx;
        vec3 x1 = x0 - i1 + C.xxx;
        vec3 x2 = x0 - i2 + C.yyy; // 2.0*C.x = 1/3 = C.y
        vec3 x3 = x0 - D.yyy;      // -1.0+3.0*C.x = -0.5 = -D.y

        // Permutations
        i = mod289(i);
        vec4 p = permute( permute( permute(
        i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
        + i.y + vec4(0.0, i1.y, i2.y, 1.0 ))
        + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));

        // Gradients: 7x7 points over a square, mapped onto an octahedron.
        // The ring size 17*17 = 289 is close to a multiple of 49 (49*6 = 294)
        float n_ = 0.142857142857; // 1.0/7.0
        vec3  ns = n_ * D.wyz - D.xzx;

        vec4 j = p - 49.0 * floor(p * ns.z * ns.z);  //  mod(p,7*7)

        vec4 x_ = floor(j * ns.z);
        vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)

        vec4 x = x_ *ns.x + ns.yyyy;
        vec4 y = y_ *ns.x + ns.yyyy;
        vec4 h = 1.0 - abs(x) - abs(y);

        vec4 b0 = vec4( x.xy, y.xy );
        vec4 b1 = vec4( x.zw, y.zw );

        //vec4 s0 = vec4(lessThan(b0,0.0))*2.0 - 1.0;
        //vec4 s1 = vec4(lessThan(b1,0.0))*2.0 - 1.0;
        vec4 s0 = floor(b0)*2.0 + 1.0;
        vec4 s1 = floor(b1)*2.0 + 1.0;
        vec4 sh = -step(h, vec4(0.0));

        vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
        vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;

        vec3 p0 = vec3(a0.xy,h.x);
        vec3 p1 = vec3(a0.zw,h.y);
        vec3 p2 = vec3(a1.xy,h.z);
        vec3 p3 = vec3(a1.zw,h.w);

        //Normalise gradients
        vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
        p0 *= norm.x;
        p1 *= norm.y;
        p2 *= norm.z;
        p3 *= norm.w;

        // Mix final noise value
        vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
        m = m * m;
        return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1),
        dot(p2,x2), dot(p3,x3) ) );
      }

      varying vec2 st;
      varying vec3 v_position;
      varying vec3 viewDirection;
      uniform float time;

      varying vec3 N;
      varying vec3 v;

      void main(){

        float height = 0.0;

        float newX = position.x;
        float newY = position.y;

        //Total number of waves
        const int nrWaves = 50;

        //Create the noise variable here in order to gain performance
        float noiseVar = snoise(position*0.004+time*0.2);

        //Sum the waves
        for(int i = 0; i < nrWaves; i++){
          height += (noiseVar*sin(dot(vec2(newX, newY), vec2(0.01, 0.01))+time*1.2));

          newX += dot(1.0*1.0*noiseVar, cos(dot(vec2(newX, newY), vec2(0.01, 0.01))+time*1.2));
          newY += dot(1.0*1.0*noiseVar, cos(dot(vec2(newX, newY), vec2(0.01, 0.01))+time*1.2));
        }

        //Set the new position of the vertex
        vec3 newPosition = position;
        newPosition.x = newX;
        newPosition.y = newY;
        newPosition.z = height;
        gl_Position = projectionMatrix*modelViewMatrix*vec4(newPosition, 1.0);

        st = uv;
        v_position = position;

        //View direction, used for reflecting the sky
        viewDirection = vec3(modelMatrix*vec4(position, 1.0) - vec4(cameraPosition, 1.0));

        //Other variables for specular lighting 
        v = vec3(modelViewMatrix * vec4(position, 1.0));
        N = (normalMatrix * normal);
      }

    </script>

    <!-- fragment shader -->
    <script type="x-shader/x-fragment" id="oceanFragmentShader">
      //Noise

      //
      // Description : Array and textureless GLSL 2D/3D/4D simplex
      //               noise functions.
      //      Author : Ian McEwan, Ashima Arts.
      //  Maintainer : ijm
      //     Lastmod : 20110822 (ijm)
      //     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
      //               Distributed under the MIT License. See LICENSE file.
      //               https://github.com/ashima/webgl-noise
      //

      vec3 mod289(vec3 x) {
        return x - floor(x * (1.0 / 289.0)) * 289.0;
      }

      vec4 mod289(vec4 x) {
        return x - floor(x * (1.0 / 289.0)) * 289.0;
      }

      vec4 permute(vec4 x) {
        return mod289(((x*34.0)+1.0)*x);
      }

      vec4 taylorInvSqrt(vec4 r)
      {
        return 1.79284291400159 - 0.85373472095314 * r;
      }

      float snoise(vec3 v)
      {
        const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
        const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);

        // First corner
        vec3 i  = floor(v + dot(v, C.yyy) );
        vec3 x0 =   v - i + dot(i, C.xxx) ;

        // Other corners
        vec3 g = step(x0.yzx, x0.xyz);
        vec3 l = 1.0 - g;
        vec3 i1 = min( g.xyz, l.zxy );
        vec3 i2 = max( g.xyz, l.zxy );

        //   x0 = x0 - 0.0 + 0.0 * C.xxx;
        //   x1 = x0 - i1  + 1.0 * C.xxx;
        //   x2 = x0 - i2  + 2.0 * C.xxx;
        //   x3 = x0 - 1.0 + 3.0 * C.xxx;
        vec3 x1 = x0 - i1 + C.xxx;
        vec3 x2 = x0 - i2 + C.yyy; // 2.0*C.x = 1/3 = C.y
        vec3 x3 = x0 - D.yyy;      // -1.0+3.0*C.x = -0.5 = -D.y

        // Permutations
        i = mod289(i);
        vec4 p = permute( permute( permute(
        i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
        + i.y + vec4(0.0, i1.y, i2.y, 1.0 ))
        + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));

        // Gradients: 7x7 points over a square, mapped onto an octahedron.
        // The ring size 17*17 = 289 is close to a multiple of 49 (49*6 = 294)
        float n_ = 0.142857142857; // 1.0/7.0
        vec3  ns = n_ * D.wyz - D.xzx;

        vec4 j = p - 49.0 * floor(p * ns.z * ns.z);  //  mod(p,7*7)

        vec4 x_ = floor(j * ns.z);
        vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)

        vec4 x = x_ *ns.x + ns.yyyy;
        vec4 y = y_ *ns.x + ns.yyyy;
        vec4 h = 1.0 - abs(x) - abs(y);

        vec4 b0 = vec4( x.xy, y.xy );
        vec4 b1 = vec4( x.zw, y.zw );

        //vec4 s0 = vec4(lessThan(b0,0.0))*2.0 - 1.0;
        //vec4 s1 = vec4(lessThan(b1,0.0))*2.0 - 1.0;
        vec4 s0 = floor(b0)*2.0 + 1.0;
        vec4 s1 = floor(b1)*2.0 + 1.0;
        vec4 sh = -step(h, vec4(0.0));

        vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
        vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;

        vec3 p0 = vec3(a0.xy,h.x);
        vec3 p1 = vec3(a0.zw,h.y);
        vec3 p2 = vec3(a1.xy,h.z);
        vec3 p3 = vec3(a1.zw,h.w);

        //Normalise gradients
        vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
        p0 *= norm.x;
        p1 *= norm.y;
        p2 *= norm.z;
        p3 *= norm.w;

        // Mix final noise value
        vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
        m = m * m;
        return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1),
        dot(p2,x2), dot(p3,x3) ) );
      }

      #ifdef GL_ES
      precision highp float;
      #endif

      varying vec2 st;
      varying vec3 viewDirection;
      varying vec3 v_position;

      uniform sampler2D normalMap;
      uniform samplerCube skyTexture;

      uniform vec3 pointLightColor;
  		uniform vec3 pointLightPosition;
  		uniform float pointLightDecay;

      uniform float time;


      varying vec3 N;
      varying vec3 v;

      void main(){
        //Create normal from normalMap combined with noise. 
        vec3 sampledNormal = (normalize(texture2D(normalMap, st*0.05*tan(snoise(0.11*v_position+time*2.0))).rgb*2.0-1.0) + N*0.2*sin(snoise(0.05*v_position+time)));

        vec3 L = normalize(pointLightPosition - v);
        vec3 E = normalize(-v); // we are in Eye Coordinates, so EyePos is (0,0,0)
        vec3 R = normalize(-reflect(L, sampledNormal));

        //calculate Ambient Term:
        vec4 Iamb = vec4(pointLightColor, 1.0)*pointLightDecay;

        //calculate Diffuse Term:
        vec4 Idiff = vec4(pointLightColor, 1.0)* max(dot(sampledNormal, L), 0.0);
        Idiff = clamp(Idiff, 0.0, 1.0)*pointLightDecay;

        // calculate Specular Term:
        vec4 Ispec = vec4(pointLightColor, 1.0) * pow(max(dot(R,E), 0.0), 4.0);
        Ispec = clamp(Ispec, 0.0, 1.0);

        //Calculate the reflected direction from the skybox
        vec3 reflectedDirection = normalize(reflect(viewDirection, sampledNormal));

        vec4 reflectionColor = normalize(textureCube(skyTexture, reflectedDirection));
        gl_FragColor = (reflectionColor + (Iamb + Idiff + Ispec));
      }

    </script>

    <!-- Skybox vertex Shader -->
    <script type="x-shader/x-vertex" id="skyBoxVertexShader">
      #ifdef GL_ES
      precision highp float;
      #endif

      varying vec3 v_position;

      void main(){
        gl_Position = projectionMatrix * modelViewMatrix *  vec4(position, 1.0);
        v_position = position;
      }
    </script>

    <!--Skybox fragment shader -->
    <!-- Uses a multifractal to create clouds -->
    <script type="x-shader/x-fragment" id="skyBoxFragmentShader">

      #ifdef GL_ES
      precision highp float;
      #endif


      //Noise
      vec3 mod289(vec3 x) {
        return x - floor(x * (1.0 / 289.0)) * 289.0;
      }

      vec4 mod289(vec4 x) {
        return x - floor(x * (1.0 / 289.0)) * 289.0;
      }

      vec4 permute(vec4 x) {
        return mod289(((x*34.0)+1.0)*x);
      }

      vec4 taylorInvSqrt(vec4 r)
      {
        return 1.79284291400159 - 0.85373472095314 * r;
      }

      float snoise(vec3 v)
      {
        const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
        const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);

        // First corner
        vec3 i  = floor(v + dot(v, C.yyy) );
        vec3 x0 =   v - i + dot(i, C.xxx) ;

        // Other corners
        vec3 g = step(x0.yzx, x0.xyz);
        vec3 l = 1.0 - g;
        vec3 i1 = min( g.xyz, l.zxy );
        vec3 i2 = max( g.xyz, l.zxy );

        //   x0 = x0 - 0.0 + 0.0 * C.xxx;
        //   x1 = x0 - i1  + 1.0 * C.xxx;
        //   x2 = x0 - i2  + 2.0 * C.xxx;
        //   x3 = x0 - 1.0 + 3.0 * C.xxx;
        vec3 x1 = x0 - i1 + C.xxx;
        vec3 x2 = x0 - i2 + C.yyy; // 2.0*C.x = 1/3 = C.y
        vec3 x3 = x0 - D.yyy;      // -1.0+3.0*C.x = -0.5 = -D.y

        // Permutations
        i = mod289(i);
        vec4 p = permute( permute( permute(
          i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
          + i.y + vec4(0.0, i1.y, i2.y, 1.0 ))
          + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));

          // Gradients: 7x7 points over a square, mapped onto an octahedron.
          // The ring size 17*17 = 289 is close to a multiple of 49 (49*6 = 294)
          float n_ = 0.142857142857; // 1.0/7.0
          vec3  ns = n_ * D.wyz - D.xzx;

          vec4 j = p - 49.0 * floor(p * ns.z * ns.z);  //  mod(p,7*7)

          vec4 x_ = floor(j * ns.z);
          vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)

          vec4 x = x_ *ns.x + ns.yyyy;
          vec4 y = y_ *ns.x + ns.yyyy;
          vec4 h = 1.0 - abs(x) - abs(y);

          vec4 b0 = vec4( x.xy, y.xy );
          vec4 b1 = vec4( x.zw, y.zw );

          //vec4 s0 = vec4(lessThan(b0,0.0))*2.0 - 1.0;
          //vec4 s1 = vec4(lessThan(b1,0.0))*2.0 - 1.0;
          vec4 s0 = floor(b0)*2.0 + 1.0;
          vec4 s1 = floor(b1)*2.0 + 1.0;
          vec4 sh = -step(h, vec4(0.0));

          vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
          vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;

          vec3 p0 = vec3(a0.xy,h.x);
          vec3 p1 = vec3(a0.zw,h.y);
          vec3 p2 = vec3(a1.xy,h.z);
          vec3 p3 = vec3(a1.zw,h.w);

          //Normalise gradients
          vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
          p0 *= norm.x;
          p1 *= norm.y;
          p2 *= norm.z;
          p3 *= norm.w;

          // Mix final noise value
          vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
          m = m * m;
          return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1),
          dot(p2,x2), dot(p3,x3) ) );
        }

      varying vec3 v_position;
      uniform float time;

      vec4 spaceClouds(vec3 cubePos){
        //Normalize the cube position to map it on to a sphere.
        vec3 spherePos = normalize(cubePos);
        
        const int octaves = 8;
        float sum = 0.0;
        float frequency = 0.7;
        float weight = 1.3;

        for(int i = 0; i < octaves; ++i){
          sum = sum + weight*snoise(frequency*spherePos+time*0.1);
          frequency = 2.0*frequency;
          weight = 0.5*weight;
        }
        sum = sum/float(octaves);
        float val = 0.5+0.5*sum;
        val = pow(1.8*val, 12.0);

        //Return the resulting color vector.
        return vec4(0.5*val*val*val, 0.5*val*val, 0.5*val, 1.0);
      }

      void main(){
        gl_FragColor = spaceClouds(v_position);
      }

    </script>

    
    <div id="container">
      <div id="header">
        <h1>Procedural Ocean in Space</h1>
        <h2>A project by John Holl&#233;n</h2>
      </div>
      <div id="canvasWrapper"></div>
      <div id="content">
        <h2>Project Report</h2>

        <h3>Introduction</h3>
        <p>
          This project has been made by John Holl&#233;n (johho982) in the course <b>TNM084 - Procedural Images</b> at Link&ouml;ping University. The original plan was to create a procedurally generated planet, but it proved to be too challenging for me as I am not particularly skilled at OpenGL/WebGL. The plan was changed to creating an ocean instead, an ocean in space.
          <br><br>
          I chose to do the project in WebGL because web programming is my favorite kind of programming, and the idea of running 3D-graphics in the browser is such a cool one. 
          <br><br>
          This application is tested and works fine in Safari, Chrome and Firefox on a 2011 MacBook Pro. The implementation of 3D simplex noise used in this application is fetched from [2].
        </p>

        <h3>Frameworks and libraries</h3>
        <p>
          The application is written using the following libraries: 
          <ul>
            <li><b>Three.js - </b> for simplifying the process of setting up a scene.</li>
            <li><b>JQuery - </b> for DOM manipulation and loading the shaders.</li>
            <li><b>GLSL - </b> for writing shaders.</li>
          </ul>
        </p>

        <h3>Basic Scene Setup</h3>
        <p>
          The scene only consists of two objects. A big cube used as a sky box, and a plane consisting of 100x50 triangles used as the water surface. Both these objects are created by using their respective THREE.js method, THREE.CubeGeometry and THREE.PlaneGeometry. There is only one light source in the scene, a THREE.PointLight. A point light is a simple light source that does not have any direction, it only has a distance and an intensity.
          <br><br>
          There are also two cameras in the scene. A THREE.PerspectiveCamera which is the one we are looking through, and a THREE.CubeCamera which is used for reflecting the sky in the water. How the reflecting of the sky is done will be described later.
        </p>

        <h3>Implementing the Sky</h3>
        <p>
          The first idea was to create a planet, therefore the space environment was created first. First the big cube was created and scaled to a size of 10000x10000x10000 and configured to only show its inner sides. In order to attach a shader program to the sky box, a THREE.ShaderMaterial was used. The shader material-object takes shader programs as input and does all the shader binding and compiling in the background. The vertex shader for the sky box is not really interesting since it only provides the fragment shader with the vertex positions. 
          <br><br>
          The fragment shader however, is a little more interesting. The sky texture is rendered using an eight-octave multifractal [1] that also uses the time in order for the sky to move. The full code for the sky texture is shown below.

<!-- Pre tag --> 
<pre>
<code>
varying vec3 v_position;
uniform float time;

vec4 spaceClouds(vec3 cubePos){
  //Normalize the cube position to map it on to a sphere.
  vec3 spherePos = normalize(cubePos);
  
  const int octaves = 8;
  float sum = 0.0;
  float frequency = 0.7;
  float weight = 1.3;

  for(int i = 0; i &lt; octaves; ++i){
    sum = sum + weight*snoise(frequency*spherePos+time*0.1);
    frequency = 2.0*frequency;
    weight = 0.5*weight;
  }
  sum = sum/float(octaves);
  float val = 0.5+0.5*sum;
  val = pow(1.8*val, 12.0);

  //Return the resulting color vector.
  return vec4(0.5*val*val*val, 0.5*val*val, 0.5*val, 1.0);
}

void main(){
  gl_FragColor = spaceClouds(v_position);
}
</code>
</pre>
<!-- End Pre tag --> 
          
          The values for <i>frequency</i> and <i>weight</i> were found by trial and error until a satisfying result was found. When a satisfying result was found I went on with implementing the ocean.
        </p>

        <h3>Implementing the Ocean</h3>
        <p>
          As mentioned before, the water surface consists of a plane with 100x50 triangles. The first thing was to configure the water surface to render as a wire frame. When that was done the work with creating wave forms begun. I wanted to create and ocean, therefore I wanted rather large wave forms. To achieve ocean like wave forms, Gerstner waves were implemented in the vertex shader of the water surface.
        </p>

        <h4>Gerstner Waves</h4>
        <p>
          Regular sine waves has a rounded top, but water waved has a more pointy top. This is were the Gerstner waves come in. Gerstner waves are a sum of sine waves of different amplitudes and frequencies. The Gerstner wave function looks as follows [3]: 
    
          <figure style="text-align:center">
            <img src="./Imgs/gerstner.png" alt="gerstner function">
            <figcaption>
              Equation 1 - The Gerstner wave function. 
            </figcaption>
          </figure>
        </p>
        <p>
          Where <i>A</i> is the amplitude, <i>&omega;</i> is the frequency, and <i>&phi;</i> is the phase constant. <i>Q</i> is a parameter that determines the steepness of the wave [3]. This function will not only displace the height of the wave but also the <i>x</i> and <i>y</i> coordinates of the vertex.
          <br><br>
          Instead of using predefined waves in the wave function I used simplex noise of the vertex position as the parameters <i>Q</i> and <i>A</i> in order to achieve waves that have a random behavior. This turned out to look quite good, and after all, it is the visual effect I am after.
        </p> 
        <h4>The Resulting Vertex Shader</h4>
        <p>
          The resulting vertex shader for the water surface is shown below: 

<!-- Pre tag -->
<pre>
<code>
varying vec2 st;
varying vec3 v_position;
varying vec3 viewDirection;
uniform float time;

varying vec3 N;
varying vec3 v;

void main(){

  float height = 0.0;

  float newX = position.x;
  float newY = position.y;

  //Total number of waves
  const int nrWaves = 50;

  //Create the noise variable here in order to gain performance
  float noiseVar = snoise(position*0.004+time*0.2);

  //Sum the waves
  for(int i = 0; i < nrWaves; i++){
    height += (noiseVar*sin(dot(vec2(newX, newY), vec2(0.01, 0.01))+time*1.2));

    newX += dot(1.0*1.0*noiseVar, cos(dot(vec2(newX, newY), vec2(0.01, 0.01))+time*1.2));
    newY += dot(1.0*1.0*noiseVar, cos(dot(vec2(newX, newY), vec2(0.01, 0.01))+time*1.2));
  }

  //Set the new position of the vertex
  vec3 newPosition = position;
  newPosition.x = newX;
  newPosition.y = newY;
  newPosition.z = height;
  gl_Position = projectionMatrix*modelViewMatrix*vec4(newPosition, 1.0);

  st = uv;
  v_position = position;

  //View direction, used for reflecting the sky
  viewDirection = vec3(modelMatrix*vec4(position, 1.0) - vec4(cameraPosition, 1.0));

  //Other variables for specular lighting 
  v = vec3(modelViewMatrix * vec4(position, 1.0));
  N = (normalMatrix * normal);
}
</code>
</pre>
<!-- End pre tag -->
          
          When this shader was implemented, the following was what I had: 
          <figure>
            <img src="Imgs/oceanWireFrame.png" alt="oceanWireFrame">
            <figcaption>
              Figure 1 - The water with waves rendered as a wire frame. The sky box has been disabled.
            </figcaption>
          </figure>
        </p>
        <p>
          Now I had the waves of the water surface, but the surface it self was still completely flat so I went on with creating a ripply surface. This was done in the fragment shader of the water surface.           
        </p>
        <p>
          The first thing that was done in the fragment shader was to implement Phong Shading using the technique in [4]. Now I had a nice looking lighting model, but the water still did not have any ripples. In order add ripples to the water surface I first attempted to use a normal map that I borrowed from [5], but I was never able to achieve a good result by simply sampling the normals directly. I instead came up with a homebrewed solution. I simply started fiddling around with noise together with the texture coordinates when I sampled the normal map. There is really nothing scientific about it, and the math is probably wrong. But the result is quite convincing.
          <br><br>
          As I mentioned in the beginning, the sky is also reflected in the water. The reflection was of the sky was made by using the THREE.CubeCamera mentioned previously. The cube camera will map the scene on to a cube, and its render target can be used as a texture to send in to a shader program as a uniform textureCube. By first calculating the viewDirection in the shader, the color can be extracted from the textureCube using the viewDirection. The total color of the water surface is then <i>reflectedColor + ambientLight + diffuseLight + specularLight</i>.
          </p>
          <h4>The Resulting Fragment Shader</h4>
          <p>
            The resulting fragment shader is shown below.
<pre>
<code>
varying vec2 st;
varying vec3 viewDirection;
varying vec3 v_position;

uniform sampler2D normalMap;
uniform samplerCube skyTexture;

uniform vec3 pointLightColor;
uniform vec3 pointLightPosition;
uniform float pointLightDecay;

uniform float time;


varying vec3 N;
varying vec3 v;

void main(){
  //Create normal from normalMap combined with noise. 
  vec3 sampledNormal = (normalize(texture2D(normalMap, st*0.05*tan(snoise(0.11*v_position+time*2.0))).rgb*2.0-1.0) + N*0.2*sin(snoise(0.05*v_position+time)));

  vec3 L = normalize(pointLightPosition - v);
  vec3 E = normalize(-v); // we are in Eye Coordinates, so EyePos is (0,0,0)
  vec3 R = normalize(-reflect(L, sampledNormal));

  //calculate Ambient Term:
  vec4 Iamb = vec4(pointLightColor, 1.0)*pointLightDecay;

  //calculate Diffuse Term:
  vec4 Idiff = vec4(pointLightColor, 1.0)* max(dot(sampledNormal, L), 0.0);
  Idiff = clamp(Idiff, 0.0, 1.0)*pointLightDecay;

  // calculate Specular Term:
  vec4 Ispec = vec4(pointLightColor, 1.0) * pow(max(dot(R,E), 0.0), 4.0);
  Ispec = clamp(Ispec, 0.0, 1.0);

  //Calculate the reflected direction from the skybox
  vec3 reflectedDirection = normalize(reflect(viewDirection, sampledNormal));

  vec4 reflectionColor = normalize(textureCube(skyTexture, reflectedDirection));
  gl_FragColor = reflectionColor + (Iamb + Idiff + Ispec);
}
</code>
</pre> 
        </p>
        <h3>Result</h3>
        <p>
          The result is at the top of the page. It is to my eyes a convincing ocean that is pleasant to look at, even though the scene itself might be unrealistic. It is also rather lightweight, running at 30 FPS on my 2011 MacBook Pro with an integrated Intel HD Graphics 3000 512 MB in Safari.
        </p>
        <h3>References</h3>
        <p>
          <ol>
            <li>
              Alfons Christian, <i>Real-time Procedural Planets</i>, <a target="_blank" href="http://www.student.itn.liu.se/%7Echral647/tnm084/tnm084-2011-real-time_procedural_planets-chral647.pdf">http://www.student.itn.liu.se/%7Echral647/tnm084/tnm084-2011-real-time_procedural_planets-chral647.pdf</a>, Accessed: 2015-09-11.
            </li>
            <li>
              Ian McEwan, Ashima Arts, <a target="_blank" href="https://github.com/ashima/webgl-noise/blob/master/src/noise3D.glsl">https://github.com/ashima/webgl-noise/blob/master/src/noise3D.glsl</a>, Accessed: 2015-09-11.
            </li>
            <li>
              NVIDIA, <i>Chapter 1. Effective Water Simulation from Physical Models</i>, <a target="_blank" href="http://http.developer.nvidia.com/GPUGems/gpugems_ch01.html">http://http.developer.nvidia.com/GPUGems/gpugems_ch01.html</a>, Accessed: 2015-09-11.
            </li>
            <li>
            OpenGL, <i>Per Fragment Lighting</i>, <a target="_blank" href="https://www.opengl.org/sdk/docs/tutorials/ClockworkCoders/lighting.php">https://www.opengl.org/sdk/docs/tutorials/ClockworkCoders/lighting.php</a>, <br>Accessed: 2015-09-12.
            </li> 
            <li>Water normal texture: <a target="_blank" href="https://github.com/jbouny/ocean/tree/master/assets/img">https://github.com/jbouny/ocean/tree/master/assets/img</a>, Accessed: 2015-09-12.</li>
          </ol> 
        </p>
      </div>
    </div>
  </body>


  <!-- Scripts -->
  <script type="text/javascript" src="libs/jquery-1.11.1.min.js"></script>
  <script type="text/javascript" src="libs/three.js"></script>
  <script type="text/javascript" src="libs/UniformsLib.js"></script>
  <script type="text/javascript" src="libs/PointerLockControls.js"></script>
  <script type="text/javascript" src="libs/stats.min.js"></script>
  <script src="//cdnjs.cloudflare.com/ajax/libs/highlight.js/8.8.0/highlight.min.js"></script>


  <!-- Own Scripts -->
  <script type="text/javascript" src="scripts/main.js"></script>

  <!-- For highlighting code in the DOM -->
  <script type="text/javascript">
    $(document).ready(function() {
      $('pre code').each(function(i, block) {
        hljs.highlightBlock(block);
      });
    });
  </script>

</html>
